/*
| Pulse sequence to measure 19F R2 via CPMG
| including an anti-ringing sequence as described in
| "Small-molecule Binding to an Intrinsically Disordered Protein 
| Revealed by Experimental NMR 19F Transverse Spin-relaxation" 
| by Heller, Shukla, Figueiredo, and Hansen.
*/

/*--------------------------------
;     Parameters to set
; -------------------------------*/
#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>
/*--------------------------------
;        define loop counter
; -------------------------------*/
define list<loopcounter> ncyc_cp = <$VCLIST>
define delay Tau_cpmg
    "Tau_cpmg = d17 - p1"

"l2=0"	; loopcounter for CPMG experiments in vclist
"l5=0"	; counter for number of pi pulses in actual CPMG
"l6=0"	; counter for number of pi pulses during compensation
"p2=p1*2"
"d11=30m"
"d13=4u"

1 ze
 3m st0

 if "Tau_cpmg < 125u" 
{
  4u
  print "Tau_cpmg delay too short!"
  goto HaltAcqu
}

2 6m
3 6m
4 6m


/* ---------------------------------
;     calculation of delay
; --------------------------------*/

  "l5 = trunc(ncyc_cp[l2]+0.3)"
  4u pl1:f1
  30m

/*--------------------------------
; calculation number of
; heating compensation pulses
; for the next experiment
; -------------------------------*/
   "l6 = l7 - l5"
  if "l5 > 60"
{
  4u
  print " Number of ncyc(l5, CPMG) too large. ncyc < 61"
  goto HaltAcqu
}
  if "l6 > 60"
{
  4u
  print " Number of ncyc(l6, Heat Comp) too large. ncyc < 61"
  goto HaltAcqu
}

/* ---------------------------------
;     heating compensation
; --------------------------------*/

  if "l6 > 0"
  {
    
5    Tau_cpmg
     (p1*2 ph2):f1
     Tau_cpmg
  lo to 5 times l6

6    Tau_cpmg
     (p1*2 ph2):f1
     Tau_cpmg
  lo to 6 times l6
  }

100u

/* ---------------------------------
;     add extra pi pulses
; to avoid starting magnetization
; --------------------------------*/
extral,125u
      (p1*2 ph2):f1
      125u
      lo to extral times 20

/* ---------------------------------
;     this is the start
; --------------------------------*/
7 d1
  20u UNBLKGRAD

  4u
  p16:gp1
  d16

  (p1 ph1):f1

/* ---------------------------------
;     CPMG block
; --------------------------------*/

if "l5 > 0"
{
8  Tau_cpmg
   (p1*2 ph2):f1
   Tau_cpmg
lo to 8 times l5
}

if "l5 > 0"
{

9  Tau_cpmg
   (p1*2 ph2):f1
   Tau_cpmg
lo to 9 times l5
}

p1 ph3

4u
p16:gp2
d16

4u BLKGRAD

/* ---------------------------------
;     anti-ringing
; --------------------------------*/

d13
(p1 ph1):f1
d13
(p1 ph4):f1
d13
(p1 ph5):f1

goscnp ph31

3m 
iu2

d11 st 
lo to 2 times nbl

ru2

3m ipp1 ipp2 ipp3 ipp4 ipp5 ipp31
lo to 3 times ns

d11 wr #0    ; write the data
3m rpp1 rpp2 rpp3 rpp4 rpp5 rpp31
3m zd        ; clear the memory

lo to 4 times td0

HaltAcqu, 1m
exit


ph1= 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3
ph2= 1 1 1 1 1 1 1 1 3 3 3 3 3 3 3 3 1 1 1 1 1 1 1 1 3 3 3 3 3 3 3 3 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph3= 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
ph4= 2 0 2 0 2 0 2 0 2 0 2 0 2 0 2 0 0 2 0 2 0 2 0 2 0 2 0 2 0 2 0 2 3 1 3 1 3 1 3 1 3 1 3 1 3 1 3 1 1 3 1 3 1 3 1 3 1 3 1 3 1 3 1 3
ph5= 0 0 2 2 1 1 3 3 0 0 2 2 1 1 3 3 2 2 0 0 3 3 1 1 2 2 0 0 3 3 1 1 1 1 3 3 2 2 0 0 1 1 3 3 2 2 0 0 3 3 1 1 0 0 2 2 3 3 1 1 0 0 2 2
ph31=0 2 2 0 1 3 3 1 0 2 2 0 1 3 3 1 2 0 0 2 3 1 1 3 2 0 0 2 3 1 1 3 1 3 3 1 2 0 0 2 1 3 3 1 2 0 0 2 3 1 1 3 0 2 2 0 3 1 1 3 0 2 2 0

;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O 			[30 msec]
;d17: CPMG delay tau [ tau - pi - tau]
;d20: fixed echo time
;l7:  Maximum of vclist
;vc : variable loop counter, taken from vc-list
;ns: 8 * n or 64*n
;ds: > 128
;td1: number of experiments = number of values in vc-list
;VCLIST: List with number of cycles ( tau - pi - tau)2
;d20: decreases probe ringdown even in the absence of aring
